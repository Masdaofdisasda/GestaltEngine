#version 450

#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_shader_16bit_storage : require

layout(local_size_x = 8, local_size_y = 8) in;

// set 0
#include "per_frame_structs.glsl"
// set 1
//layout(set = 1, binding = 0) uniform samplerCube texEnvMap;
//layout(set = 1, binding = 1) uniform samplerCube texEnvMapIrradiance;
//layout(set = 1, binding = 2) uniform sampler2D texBdrfLut;

struct MaterialConstants {
    uint16_t albedo_tex_index;
    uint16_t metal_rough_tex_index;
    uint16_t normal_tex_index;
    uint16_t emissive_tex_index;
    uint16_t occlusion_tex_index;
    uint textureFlags;

    vec4 albedo_factor;
    vec2 metal_rough_factor;
    float occlusionStrength;
    float alpha_cutoff;
    vec3 emissiveColor;
    float emissiveStrength; 
};

layout(std430, set = 1, binding = 4) readonly buffer MaterialData {
    MaterialConstants materialData[256];
};

#include "pbr.glsl"
#include "shadow_mapping.glsl"
#include "volumetric_light_util.glsl"

struct ViewProjData {
	mat4 view;
    mat4 proj;
}; 

struct DirectionalLight {
	vec3 color;
	float intensity;
	vec3 direction;
	int viewProjIndex;
}; 

struct PointLight {
	vec3 color;
	float intensity;
	vec3 position;
	float range;
}; 

layout(set = 2, binding = 0) readonly buffer LightViewProj
{
	ViewProjData viewProjData[];
};

layout(set = 2, binding = 1) readonly buffer DirectionalLightBuffer
{
	DirectionalLight dirLight[];
};

layout(set = 2, binding = 2) readonly buffer PointLightBuffer
{
	PointLight pointLight[];
};

layout(set = 3, binding = 0) uniform sampler2D gbuffer1;
layout(set = 3, binding = 1) uniform sampler2D gbuffer2;
layout(set = 3, binding = 2) uniform sampler2D gbuffer3;
layout(set = 3, binding = 3) uniform sampler2D depthBuffer;
layout(set = 3, binding = 4) uniform sampler2D shadowMap;
layout(set = 3, binding = 5) uniform sampler3D integrated_light_scattering_texture;
layout(set = 3, binding = 6) uniform sampler2D texAo;
layout(set = 3, binding = 7, rgba16f) writeonly uniform image2D outputImage;

layout( push_constant ) uniform constants
{	
	mat4 invViewProj;
    int debugMode;
    int num_dir_lights;
    int num_point_lights;
    int shading_mode;
    int shadow_mode;
    int ibl_mode;
} params;

// Function to reconstruct world position from depth
vec3 ReconstructWorldPos(float depth, vec2 uv, mat4 invViewProj) {
    vec4 clipSpacePosition = vec4(uv * 2.0 - 1.0, depth, 1.0);
    vec4 worldSpacePosition = invViewProj * clipSpacePosition;
    return worldSpacePosition.xyz / worldSpacePosition.w;
}

// https://johnwhite3d.blogspot.com/2017/10/signed-octahedron-normal-encoding.html
vec3 SignedOctDecode(vec3 n) {

    // Perform the reverse octahedral transformation
    vec3 OutN;
    OutN.x = (n.x - n.y);
    OutN.y = (n.x + n.y) - 1.0;

    // Retrieve the sign of the z component
    OutN.z = n.z * 2.0 - 1.0;

    // Reconstruct the z component
    OutN.z = OutN.z * (1.0 - abs(OutN.x) - abs(OutN.y));

    OutN = normalize(OutN);

    return OutN;
}

const mat4 biasMat = mat4( 
	0.5, 0.0, 0.0, 0.0,
	0.0, 0.5, 0.0, 0.0,
	0.0, 0.0, 1.0, 0.0,
	0.5, 0.5, 0.0, 1.0 );

void main() {


	ivec2 texSize = imageSize(outputImage);
	ivec2 pixel_coord = ivec2(gl_GlobalInvocationID.xy);
	if (gl_GlobalInvocationID.x >= texSize.x || gl_GlobalInvocationID.y >= texSize.y) {
		return;
	}
    vec2 uv = vec2(pixel_coord) / texSize;

	vec4 albedoMetal = texture(gbuffer1, uv);
	vec4 normalRough = texture(gbuffer2, uv);
	vec4 emissiveOcclusion  = texture(gbuffer3, uv);
	float ssao = texture(texAo, uv).r;

	vec4 Kd = vec4(albedoMetal.rgb, 1.0);

	vec3 n = SignedOctDecode(normalRough.rgb);

	vec4 Ke = vec4(emissiveOcclusion.rgb, 1.0);
	
	vec4 MeR = vec4(emissiveOcclusion.a, 
					normalRough.a,
					albedoMetal.a,
					1.0);

    float depth = texture(depthBuffer, uv).r;
    vec3 worldPos = ReconstructWorldPos(depth, uv, params.invViewProj);
	vec3 viewPos = -normalize(vec3(view[0][2], view[1][2], view[2][2]));

    // Transform world position to light space
	mat4 lightView = viewProjData[dirLight[0].viewProjIndex].view;
	mat4 lightProj = viewProjData[dirLight[0].viewProjIndex].proj;
	mat4 lightViewProj = lightProj * lightView;
    vec4 lightSpacePos = biasMat * lightViewProj * vec4(worldPos, 1.0);

	float shadow = 1.0;
	if (params.shadow_mode == 0) {
		float bias = calculateDynamicBias(n, dirLight[0].direction);
		shadow = shadowFactor(lightSpacePos, shadowMap, bias).r;
	}

	MaterialInfo materialInfo;
	calculatePBRInputsMetallicRoughness(Kd, n, viewPos.xyz, worldPos, MeR, materialInfo);

	// Calculate lighting contribution from image based lighting source (IBL)
	/*
	if (params.ibl_mode == 0) {
		materialInfo.f_diffuse_ibl += getIBLRadianceLambertian(materialInfo, texBdrfLut, texEnvMapIrradiance);
		materialInfo.f_specular_ibl += getIBLRadianceGGX(materialInfo, texBdrfLut, texEnvMap);
	} else if (params.ibl_mode == 1) {
		materialInfo.f_diffuse_ibl += getIBLRadianceLambertian(materialInfo, texBdrfLut, texEnvMapIrradiance);
	} else if (params.ibl_mode == 2) {
		materialInfo.f_specular_ibl += getIBLRadianceGGX(materialInfo, texBdrfLut, texEnvMap);
	}*/

	// directional light contribution
	if (params.shading_mode == 0 || params.shading_mode == 1) {
		for (int i = 0; i < params.num_dir_lights; ++i) {

			vec3 D = normalize(dirLight[i].direction);

			// BSTF
			vec3 n = materialInfo.n;
			vec3 v = materialInfo.v;
			
			// https://seblagarde.wordpress.com/wp-content/uploads/2015/07/course_notes_moving_frostbite_to_pbr_v32.pdf
			float sunAngularRadius = 0.0046;
			float r = sin(sunAngularRadius); // Disk radius
			float d = cos(sunAngularRadius); // Distance to disk

			// Compute the reflection vector
			vec3 R = reflect(-v, n);

			// Adjusted light direction for specular lighting
			float DdotR = dot(D, R);
			vec3 S = R - DdotR * D;
			vec3 L = DdotR < d ? normalize(d * D + normalize(S) * r) : R;

			vec3 h = normalize(L + v);          // Direction of the vector between l and v, called halfway vector
			float NdotL = clamp(dot(n, L), 0.001, 1.0);
			float NdotV = clamp(dot(n, v), 0.0, 1.0);
			float NdotH = clamp(dot(n, h), 0.0, 1.0);
			float LdotH = clamp(dot(L, h), 0.0, 1.0);
			float VdotH = clamp(dot(v, h), 0.0, 1.0);
			float NdotD = clamp(dot(n, D), 0.0, 1.0);

			if (NdotL <= 0.0 && NdotV <= 0.0) { continue; }

			float illuminance = dirLight[i].intensity * NdotD;

			vec3 intensity = getDirectionalLighIntensity(dirLight[i].color, illuminance);
			materialInfo.f_diffuse += shadow * intensity * NdotD *  BRDF_lambertian(materialInfo.f0, materialInfo.f90, materialInfo.c_diff, materialInfo.specularWeight, VdotH);
			materialInfo.f_specular += shadow * intensity * NdotL * BRDF_specularGGX(materialInfo.f0, materialInfo.f90, materialInfo.alphaRoughness, materialInfo.specularWeight, VdotH, NdotL, NdotV, NdotH);
		}
	}

	// point light contribution
	if (params.shading_mode == 0 || params.shading_mode == 2) {
		for (int i = 0; i < params.num_point_lights; ++i) {

			vec3 pointToLight = pointLight[i].position - worldPos;

			// BSTF
			vec3 n = materialInfo.n;
			vec3 v = materialInfo.v;
			vec3 l = normalize(pointToLight);   // Direction from surface point to light
			vec3 h = normalize(l + v);          // Direction of the vector between l and v, called halfway vector
			float NdotL = clamp(dot(n, l), 0.001, 1.0);
			float NdotV = clamp(dot(n, v), 0.0, 1.0);
			float NdotH = clamp(dot(n, h), 0.0, 1.0);
			float LdotH = clamp(dot(l, h), 0.0, 1.0);
			float VdotH = clamp(dot(v, h), 0.0, 1.0);

			if (NdotL <= 0.0 && NdotV <= 0.0) { continue; }

			// Calculation of analytical light
			// https://github.com/KhronosGroup/glTF/tree/master/specification/2.0#acknowledgments AppendixB
			vec3 intensity = getPointLighIntensity(pointLight[i].color, pointLight[i].intensity, pointLight[i].range, pointToLight);
			materialInfo.f_diffuse += intensity * NdotL *  BRDF_lambertian(materialInfo.f0, materialInfo.f90, materialInfo.c_diff, materialInfo.specularWeight, VdotH);
			materialInfo.f_specular += intensity * NdotL * BRDF_specularGGX(materialInfo.f0, materialInfo.f90, materialInfo.alphaRoughness, materialInfo.specularWeight, VdotH, NdotL, NdotV, NdotH);
		}
	}

	materialInfo.f_emissive = Ke.rgb;

	float ao = MeR.r + ssao;
	vec3 diffuse = materialInfo.f_diffuse + materialInfo.f_diffuse_ibl * ao;
	vec3 specular = materialInfo.f_specular + materialInfo.f_specular_ibl * ao * shadow;

	vec3 color = materialInfo.f_emissive + diffuse + specular;

	vec2 uv_inv = vec2(uv.x, 1.0 - uv.y); // TODO : fog volume is somehow flipped
	color = apply_volumetric_fog( uv_inv, depth, color, znear, zfar, integrated_light_scattering_texture );

	vec4 outColor = vec4(color, 1.0);

	if (params.debugMode == 0) {
		outColor = vec4(color, 1.0);
	} else if (params.debugMode == 1) {
		outColor = vec4(Kd.rgb, 1.0);
	} else if (params.debugMode == 2) {
		outColor = vec4(n * 0.5 + 0.5, 1.0);
	} else if (params.debugMode == 3) {
		outColor = vec4(Ke.rgb, 1.0);
	} else if (params.debugMode == 4) {
		outColor = vec4(MeR.rgb, 1.0);
	} else if (params.debugMode == 5) {
		outColor = vec4(ao.rrr, 1.0);
	} else if (params.debugMode == 6) {
		outColor = vec4(0.0, MeR.g, 0.0, 1.0);
	} else if (params.debugMode == 7) {
		outColor = vec4(0.0, 0.0, MeR.b, 1.0);
	} else if (params.debugMode == 8) {
		outColor = vec4(normalize(worldPos) * 0.5 + 0.5, 1.0);
	} else if (params.debugMode == 9) {
		float normalizedDepth = depth * 2.0 - 1.0; // Assuming depth is in [0,1], map it to [-1,1]
		outColor = vec4(vec3(normalizedDepth), 1.0); // Visualize depth as grayscale color
	} else if (params.debugMode == 10) {
		vec3 normalizedLightSpacePos = lightSpacePos.xyz / lightSpacePos.w;
		outColor = vec4(normalize(normalizedLightSpacePos) * 0.5 + 0.5, 1.0);
	}

	//outColor = vec4(ao.rrr * 0.5 * Kd.rgb * max(shadow, 0.1), 1.0);
	imageStore(outputImage, pixel_coord, outColor);

}